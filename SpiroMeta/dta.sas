/*26-7-2017 MRC-Epid JHZ*/

proc import datafile='dta.dta' dbms=dta out=check(where=(ethnic=1));
run;
%macro invnorm(trait,rhs,by);
proc sort data=check;
     by &by;
run;
proc reg data=check;
     model &trait=&rhs;
     output out=r r=r&trait;
     by &by;
run;
proc rank data=r out=check normal=blom;
     var r&trait;
     ranks &trait._&by;
     by &by;
run;
proc print data=check (obs=10);
run;
%mend;
%invnorm(fev, age agesq height PC1-PC4, sex);
%invnorm(fvc, age agesq height PC1-PC4, sex);
%invnorm(pef, age agesq height PC1-PC4, sex);
%invnorm( ff, age agesq height PC1-PC4, sex);
data sex;
     set check;
     array v fev_sex fvc_sex pef_sex ff_sex;
     do over v;
        if v=. then v=-9;
     end;
run;
data _null_;
     set sex(rename=(fev_sex=fev_males fvc_sex=fvc_males pef_sex=pef_males ff_sex=ff_males) where=(sex=1));
     file 'males.dat' dlm='09'x;
     if _N_=1 then put "FID" '09'x "IID" '09'x "fev_males" '09'x "fvc_males" '09'x "pef_males" '09'x "ff_males";
     put FID IID fev_males fvc_males pef_males ff_males;
run;
data _null_;
     set sex(rename=(fev_sex=fev_females fvc_sex=fvc_females pef_sex=pef_females ff_sex=ff_females) where=(sex=2));
     file "females.dat" dlm='09'x;
     if _N_=1 then put "FID" '09'x "IID" '09'x "fev_females" '09'x "fvc_females" '09'x "pef_females" '09'x "ff_females";
     put FID IID fev_females fvc_females pef_females ff_females;
run;
%invnorm(fev, sex age agesq height PC1-PC4, smk);
%invnorm(fvc, sex age agesq height PC1-PC4, smk);
%invnorm(pef, sex age agesq height PC1-PC4, smk);
%invnorm( ff, sex age agesq height PC1-PC4, smk);
data smk;
     set check;
     array v fev_smk fvc_smk pef_smk ff_smk;
     do over v;
        if v=. then v=-9;
     end;
run;
data _null_;
     set smk(rename=(fev_smk=fev_nonsmk fvc_smk=fvc_nonsmk pef_smk=pef_nonsmk ff_smk=ff_nonsmk) where=(smk=1));
     file 'nonsmk.dat' dlm='09'x;
     if _N_=1 then put "FID" '09'x "IID" '09'x "fev_nonsmk" '09'x "fvc_nonsmk" '09'x "pef_nonsmk" '09'x "ff_nonsmk";
     put FID IID fev_nonsmk fvc_nonsmk pef_nonsmk ff_nonsmk;
run;
data _null_;
     set smk (where=(smk=2));
     file 'smk.dat' dlm='09'x;
     if _N_=1 then put "FID" '09'x "IID" '09'x "fev_smk" '09'x "fvc_smk" '09'x "pef_smk" '09'x "ff_smk";
     put FID IID fev_smk fvc_smk pef_smk ff_smk;
run;

libname x '.';
data x.check;
     set check;
run;
%macro invnormPY(trait,rhs,by);
proc sort data=check (where=(smk=2 & fev+fev+packyear2010^=.));
     by &by;
run;
proc reg data=check;
     model &trait=&rhs;
     output out=r r=r&trait;
     by &by;
run;
proc rank data=r out=check normal=blom;
     var r&trait;
     ranks &trait._&by;
     by &by;
run;
proc print data=check (obs=10);
run;
%mend;
%invnormPY(fev, sex age agesq height PC1-PC4 packyear2010, smk);
%invnormPY(fvc, sex age agesq height PC1-PC4 packyear2010, smk);
%invnormPY(pef, sex age agesq height PC1-PC4 packyear2010, smk);
%invnormPY( ff, sex age agesq height PC1-PC4 packyear2010, smk);
data _null_;
     set check(rename=(fev_smk=fev_smkPY fvc_smk=fvc_smkPY pef_smk=pef_smkPY ff_smk=ff_smkPY)
               where=(smk=2 & fev+fvc+packyear2010^=.));
     array v fev_smkPY fvc_smkPY pef_smkPY ff_smkPY;
     do over v;
        if v=. then v=-9;
     end;
     file 'smkPY.dat' dlm='09'x;
     if _N_=1 then put "FID" '09'x "IID" '09'x "fev_smkPY" '09'x "fvc_smkPY" '09'x "pef_smkPY" '09'x "ff_smkPY";
     put FID IID fev_smkPY fvc_smkPY pef_smkPY ff_smkPY;
run;
data x.smkPY;
     set check;
run;

